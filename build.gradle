import org.apache.tools.ant.filters.ReplaceTokens
buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "Forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "Sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.3'
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT' // Contains less derpy launch4j plugin
        classpath 'edu.sc.seis.gradle:macappbundle:2.0.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'macAppBundle'
apply plugin: 'maven-publish'

repositories {
	mavenCentral()
}

def grabDep(name, url){
	def f = new File('dep/' + name + ".jar")
    if(!f.exists()){
       println("  Downloading " + name + " from " + url)
       ant.get(src: url, dest: 'dep')
       file("dep/" + url.substring(url.lastIndexOf("/"))).renameTo(file("dep/" + name + ".jar"))
    }
}

dependencies {
    def f = new File('dep/')
    if(f.exists()){
        f.delete()
    }
    f.mkdirs()
    if(!f.exists()){
        f.mkdir()
    }
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'org.tukaani:xz:1.5'
    compile 'com.google.guava:guava:17.0'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'org.apache.logging.log4j:log4j-api:2.0-beta9'
    compile 'org.apache.logging.log4j:log4j-core:2.0-beta9'
    grabDep('authlib-1.5.17', 'https://libraries.minecraft.net/com/mojang/authlib/1.5.17/authlib-1.5.17.jar')
    grabDep('weblaf-complete-1.28', 'http://weblookandfeel.com/downloads/gpl/weblaf-complete-1.28.jar')
    compile files('dep/weblaf-complete-1.28.jar', 'dep/authlib-1.5.17.jar')
}


processResources{
    def ENV = System.getenv()
    if (ENV.BUILD_NUMBER) {
        filter(ReplaceTokens, tokens:['@BUILD@': '${System.getenv().BUILD_NUMBER}'])
    }
}

ext.configFile = file 'src/main/resources/build.properties'

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

mainClassName = config.mainClass
def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
	version = "${config.version.reserved}.${config.version.major}.${config.version.minor}." + "${System.getenv().BUILD_NUMBER}"
} else {
	version = "${config.version.reserved}.${config.version.major}.${config.version.minor}.0"
}

group = 'com.atlauncher'
archivesBaseName = 'OpenLauncher'
sourceCompatibility = '1.6' // tergetCompatibility set by this automatically

jar {
    manifest {
        attributes "Implementation-Version": version
        attributes "Build-Jdk": org.gradle.internal.jvm.Jvm.current()
        //attributes "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        attributes "Built-By": System.getProperty("user.name")
        attributes "Implementation-Vender": "Modmuss50"
        attributes "Created-By": "Gradle " + project.getGradle().getGradleVersion()
    }
	appendix = 'jar'
}

task copyToOutput(type: Copy) {
    from("${buildDir}/libs/")
    destinationDir = file("${buildDir}/distributions")
}

task copyMainJar(type: Copy) {
    from("${buildDir}/libs/")
    destinationDir = file("${buildDir}/macApp/ATLauncher.app/Contents/Java")
    rename { String fileName ->
        fileName.replace("-${version}", '')
    }
    rename(/(.+)-${version}(.+)/, '$1$2')
}

shadowJar  {
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    dependencies {
        exclude(dependency('junit:junit'))
        exclude(dependency('org.hamcrest:hamcrest-core'))
    }
    classifier = ''
	appendix = 'jar'
}

assemble.dependsOn.remove(createDmg)
build.dependsOn.remove(createDmg)
createApp.dependsOn.remove(copyToResourcesJava)
build.dependsOn(createAppZip)
createApp.dependsOn(copyMainJar)
copyMainJar.dependsOn(shadowJar)
build.finalizedBy(copyToOutput)


uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:///var/www/maven/")
            pom {
                groupId = "OpenLauncher"
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'OpenLauncher'
                    url 'https://github.com/OpenLauncher/OpenLauncher'
                    scm {
                        url 'https://github.com/OpenLauncher/OpenLauncher'
                        connection 'scm:git:git@github.com:OpenLauncher/OpenLauncher.git'
                        developerConnection 'scm:git:git@github.com:OpenLauncher/OpenLauncher.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/OpenLauncher/OpenLauncher/issues'
                    }
                    licenses {
                        license {
                            name 'License'
                            url 'https://github.com/OpenLauncher/OpenLauncher/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'modmuss50'
                            name 'modmuss50'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}

